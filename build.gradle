buildscript {
    ext.kotlin_version = '1.0.2'

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}


apply plugin: "com.android.library"
android {
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileSdkVersion 23
    buildToolsVersion '23.0.3'
    //noinspection GroovyAssignabilityCheck
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        androidTest.java.srcDirs += 'test-server/remote'
    }
}

apply plugin: 'kotlin-android'

apply plugin: 'com.github.dcendents.android-maven'
group='com.github.kotlin-everywhere'
// uncomment to build a jar file in addition to the default aar file
android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    artifacts.add('archives', task);
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // http://mvnrepository.com/artifact/com.google.code.gson/gson
    compile group: 'com.google.code.gson', name: 'gson', version: '2.6.2'
    compile 'com.github.kittinunf.fuel:fuel-android:1.3.1'

    // http://mvnrepository.com/artifact/junit/junit
    androidTestCompile group: 'junit', name: 'junit', version: '4.12'
    androidTestCompile 'com.android.support.test:runner:0.4'
}
